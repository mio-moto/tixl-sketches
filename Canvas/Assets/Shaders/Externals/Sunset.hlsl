/*
    from: https://fragcoord.xyz/s/c6v0vjg5
    
    "Sunset" by @XorDev
    
    Expanded and clarified version of my Sunset shader:
    https://www.shadertoy.com/view/wXjSRt
    
    Based on my tweet shader:
    https://x.com/XorDev/status/1918764164153049480
*/

//Output image brightness
#define BRIGHTNESS 1.0

//Base brightness (higher = brighter, less saturated)
#define COLOR_BASE 1.5
//Color cycle speed (radians per second)
#define COLOR_SPEED 0.5
//RGB color phase shift (in radians)
#define RGB float3(0.0, 1.0, 2.0)
//Color translucency strength
#define COLOR_WAVE 14.0
//Color direction and (magnitude = frequency)
#define COLOR_DOT float3(1,-1,0)

//Wave iterations (higher = slower)
#define WAVE_STEPS 8.0
//Starting frequency
#define WAVE_FREQ 5.0
//Wave amplitude
#define WAVE_AMP 0.6
//Scaling exponent factor
#define WAVE_EXP 1.8
//Movement direction
#define WAVE_VELOCITY float3(0.2, 0.2, 0.2)

//Cloud thickness (lower = denser)
#define PASSTHROUGH 0.2

//Cloud softness
#define SOFTNESS 0.005
//Raymarch step
#define STEPS 100.0
//Sky brightness factor (finicky)
#define SKY 10.0
//Camera fov ratio (tan(fov_y/2))
#define FOV 1.0

cbuffer Resolution : register(b0)
{
    int2 Resolution;
}

cbuffer ShaderConstants : register(b1)
{
    float Time;
}

float4 psMain(float4 fragCoord : SV_Position) : SV_Target
{
    //Raymarch depth
    float z = 0.0;

    //Step distance
    float d = 0.0;
    //Signed distance
    float s = 0.0;

    //Ray direction
    float3 dir = normalize(float3(2.0 * fragCoord - Resolution.xy, -FOV * Resolution.y));

    //Output color
    float3 col = float3(0, 0, 0);

    //Clear fragcolor and raymarch with 100 iterations
    for (float i = 0.0; i < STEPS; i++)
    {
        //Compute raymarch sample point
        float3 p = z * dir;

        //Turbulence loop
        //https://www.shadertoy.com/view/3XXSWS
        for (float j = 0.0, f = WAVE_FREQ; j < WAVE_STEPS; j++, f *= WAVE_EXP)

            p += WAVE_AMP * sin(p * f - WAVE_VELOCITY * Time).yzx / f;

        //Compute distance to top and bottom planes
        s = 0.3 - abs(p.y);
        //Soften and scale inside the clouds
        d = SOFTNESS + max(s, -s * PASSTHROUGH) / 4.0;
        //Step forward
        z += d;
        //Coloring with signed distance, position and cycle time
        float phase = COLOR_WAVE * s + dot(p,COLOR_DOT) + COLOR_SPEED * Time;
        //Apply RGB phase shifts, add base brightness and correct for sky
        col += (cos(phase - RGB) + COLOR_BASE) * exp(s * SKY) / d;
    }
    //Tanh tonemapping
    //https://www.shadertoy.com/view/ms3BD7
    col *= SOFTNESS / STEPS * BRIGHTNESS;
    col = tanh(col * col).rgb;

    col = max(float4(0, 0, 0, 0), min(col, float4(1, 1, 1, 1)));
    return float4(col.rgb, 1.0);
}
